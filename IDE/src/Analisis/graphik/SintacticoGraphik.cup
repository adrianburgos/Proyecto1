//-------------------codigo de usuario-----------------
package Analisis.graphik;
import java_cup.runtime.Symbol;
import Reportes.*;
import fabrica.*;
import ide.Const;
import semanticos.*;
parser code
{:
    public Nodo raiz = new Nodo(); 
    public void syntax_error(Symbol s)
    {
        ErroresGraphik.agregarError("Error sintactico", s.value.toString() + " no debe de ir en esa posicion", s.right, s.left);
    }

    public void unrecovered_syntax_error(Symbol s)
    {
        ErroresGraphik.agregarError("Error sintactico", "No se pudo recuperar de: " + s.value.toString(), s.right, s.left);
    }
:}

//-------------------codigo para acciones gramaticales
action code
{:

:}
//-------------------declaraciones---------------------
terminal var, nuevo, entero, decimal, caracter, cadena, bool, vacio, publico, privado, protegido;
terminal importar, als, hereda, retornar, llamarhk, llamar, inicio, incluirhk, si, sino;
terminal seleccion, defecto, caso, para, mientras, hacer, continuar, terminar, graphikarfuncion;
terminal datos, columna, procesar, donde, dondecada, dondetodo, imprimir;
terminal masmas, menosmenos, mas, menos, por, dividido, pot, and, or, xor, not, menorigual, mayorigual;
terminal menor, mayor, diferente, igualigual, interroga, dospuntos, igual, punto, coma, allave, cllave;
terminal acorchete, ccorchete, aparentesis, cparentesis;
terminal String numero, dec, iden, cad, car, verdadero, falso, extgk;

non terminal Nodo INICIO, FUN, LALS, ALS, CUERPO, HEREDA, LCUERPOALS, CUERPOALS, LIMPORTAR, VALOR;
non terminal Nodo IMPORTAR, INCLUIR, LID, ID, LVALOR, DEC, PRINCIPAL, LPAR, ASIG, ASIGNORMAL, LCORCHETES, ASIGARR;
non terminal Nodo LLLAVES, LLAVE, LCUERPO, ASIGNACION, SI, SELECCION, PARA, MIENTRAS, HACER, LLAMADO, LLAMADOHK, GRAFICAR;
non terminal Nodo SINO, LCASOS, CASO, DEFECTO, LVARIABLES, PREASIG, ASIGPARA, VALPARA, IMPRIMIR, ACCESO, REL, E;
non terminal String OPREL, VISIBILIDAD, TIPO, TIPOFUN, OP;

//precedencia menor a mayor
precedence left or;
precedence left xor;
precedence left and;
precedence left not;
precedence left menor, mayor, menorigual, mayorigual, diferente, igualigual;
precedence left mas, menos;
precedence left por, dividido;
precedence right pot;
precedence left masmas, menosmenos;
start with INICIO;
//-------------------reglas sintacticas----------------

INICIO          ::= {:System.out.println("inicio");:} INCLUIR:INCLUIR LALS:LALS
                    {:
                        Nodo RAIZ = new Nodo("raiz");
                        RAIZ.hijos.add(INCLUIR);
                        RAIZ.hijos.add(LALS);
                        raiz = RAIZ;
                        System.out.println("FINALIZO");
                    :};

LID             ::= LID:LID punto ID:id
                    {:
                        LID.hijos.add(id);
                        RESULT = (Nodo) LID;
                    :}
                |   ID:id {: RESULT = NodoGeneral.crearLid(id); :};

ID              ::= iden:nombre
                    {: RESULT = new Nodo(Const.id, nombre); :}
                |   iden:nombre aparentesis LVALOR:LVALOR cparentesis
                    {:
                        Nodo id = new Nodo(Const.id, nombre);
                        id.hijos.add(LVALOR);
                        RESULT = (Nodo) id;
                    :};

TIPO            ::= entero {: RESULT = Const.numero; :}
                |   decimal {: RESULT = Const.decimal; :}
                |   caracter {: RESULT = Const.caracter; :}
                |   cadena {: RESULT = Const.cadena; :}
                |   bool {: RESULT = Const.bool; :}
                |   iden:id {: RESULT = id; :};

LVALOR          ::= LVALOR:LVALOR coma VALOR:VALOR
                    {:
                        LVALOR.hijos.add(VALOR);
                        RESULT = (Nodo) LVALOR;
                    :}
                |   VALOR:VALOR {: RESULT = NodoGeneral.crearLvalor(VALOR); :}
                |   {: RESULT = new Nodo(Const.lvalor); :};

INCLUIR         ::= LIMPORTAR:LIMPORTAR {: RESULT = LIMPORTAR; :}
                |   {: RESULT = new Nodo(Const.limportar); :};
LIMPORTAR         ::= LIMPORTAR:LIMPORTAR IMPORTAR:IMPORTAR
                    {:
                        if(IMPORTAR != null)
                            LIMPORTAR.hijos.add(IMPORTAR);
                        RESULT = (Nodo) LIMPORTAR;
                    :}
                |   IMPORTAR:IMPORTAR 
                    {: 
                        RESULT = new Nodo(Const.limportar);
                        if(IMPORTAR != null)
                            RESULT = NodoGeneral.crearLimportar(IMPORTAR);
                    :};
IMPORTAR        ::= importar iden:id extgk:ext interroga {: RESULT = NodoGeneral.crearImportar(id, ext); :}
                |   incluirhk iden:id interroga {: RESULT = NodoGeneral.crearIncluirhk(id); :};

LALS         ::= LALS:LALS ALS:ALS
                    {:
                        if(ALS != null)
                            LALS.hijos.add(ALS);
                        RESULT = (Nodo) LALS;
                    :}
                |   ALS:ALS 
                    {: 
                        RESULT = new Nodo(Const.lals);
                        if(ALS != null)
                            RESULT = NodoGeneral.crearLals(ALS);
                    :};

ALS             ::= als iden:id HEREDA:HEREDA VISIBILIDAD:VISIBILIDAD allave LCUERPOALS:LCUERPOALS cllave
                    {:
                        RESULT = NodoGeneral.crearAls(id, VISIBILIDAD, HEREDA, LCUERPOALS);
                    :};

HEREDA          ::= hereda iden:id {: RESULT = new Nodo(Const.hereda, id); :}
                |   {: RESULT = new Nodo(Const.hereda); :};

VISIBILIDAD     ::= dospuntos publico {: RESULT = Const.publico; :}
                |   dospuntos protegido {: RESULT = Const.protegido; :}
                |   dospuntos privado {: RESULT = Const.privado; :}
                |   {: RESULT = Const.publico; :};

LCUERPOALS      ::= LCUERPOALS:LCUERPOALS CUERPOALS:CUERPOALS
                    {:
                        if(CUERPOALS != null)
                            LCUERPOALS.hijos.add(CUERPOALS);
                        RESULT = (Nodo) LCUERPOALS;
                    :}
                |   CUERPOALS:CUERPOALS
                    {:
                        RESULT = new Nodo(Const.lcuerpoals);
                        if(CUERPOALS != null) 
                            RESULT = NodoGeneral.crearLcuerpoals(CUERPOALS);
                    :};

CUERPOALS       ::= FUN:FUN {: RESULT = FUN; :}
                |   DEC:DEC interroga {: RESULT = DEC; :}
                |   PRINCIPAL:PRINCIPAL {: RESULT = PRINCIPAL; :};

FUN             ::= TIPOFUN:tipo iden:id aparentesis LPAR:LPAR cparentesis VISIBILIDAD:VISIBILIDAD allave LCUERPO:LCUERPO cllave
                    {: RESULT = NodoGeneral.crearFuncion(id, tipo, VISIBILIDAD, LPAR, LCUERPO); :};
TIPOFUN         ::= TIPO:TIPO {: RESULT = TIPO; :}
                |   vacio {: RESULT = Const.vacio; :};

LPAR            ::= LPAR:LPAR coma TIPO:TIPO iden:id
                    {:
                        int t = Semantico.getTipo(TIPO);
                        Nodo par = new Nodo(Const.id, id, t);
                        if(t == Const.tals)
                            par.tipoAls = TIPO;
                        LPAR.hijos.add(par);
                        RESULT = (Nodo) LPAR;
                    :}
                |   TIPO:TIPO iden:id {: RESULT = NodoGeneral.crearLpar(id, TIPO); :}
                |   {: RESULT = new Nodo(Const.lpar); :};

DEC             ::= var TIPO:TIPO iden:id VISIBILIDAD:VISIBILIDAD PREASIG:PREASIG
                    {:
                        RESULT = NodoGeneral.crearDec(id, TIPO, VISIBILIDAD, PREASIG);
                    :};
PREASIG         ::= LVARIABLES:LVARIABLES {: RESULT = LVARIABLES; :}
                |   ASIG:ASIG {: RESULT = ASIG; :}
                |   {: RESULT = null; :};

LVARIABLES      ::= LVARIABLES:LVARIABLES coma iden:id VISIBILIDAD:VISIBILIDAD
                    {:
                        Nodo par = new Nodo(Const.id, id);
                        par.setVisibilidad(VISIBILIDAD);
                        LVARIABLES.hijos.add(par);
                        RESULT = (Nodo) LVARIABLES;
                    :}
                |   coma iden:id VISIBILIDAD:VISIBILIDAD
                    {:
                        Nodo par = new Nodo(Const.id, id);
                        par.setVisibilidad(VISIBILIDAD);
                        Nodo lvar = new Nodo(Const.lvariables);
                        lvar.hijos.add(par);
                        RESULT = (Nodo) lvar;
                    :};
ASIG            ::= igual ASIGNORMAL:ASIGNORMAL {: RESULT = ASIGNORMAL; :}
                |   LCORCHETES:LCORCHETES ASIGARR:ASIGARR {: RESULT = NodoGeneral.crearAsigArr(LCORCHETES, ASIGARR); :};
ASIGNORMAL      ::= VALOR:VALOR {: RESULT = VALOR; :}
                |   nuevo iden:id aparentesis cparentesis {: RESULT = new Nodo(Const.nuevo, id); :};
LCORCHETES      ::= LCORCHETES:LCORCHETES acorchete VALOR:VALOR ccorchete
                    {:
                        LCORCHETES.hijos.add(VALOR);
                        RESULT = (Nodo) LCORCHETES;
                    :}
                |   acorchete VALOR:VALOR ccorchete
                    {:
                        RESULT = NodoGeneral.crearLcorchetes(VALOR);
                    :};
ASIGARR         ::= igual LLAVE:LLAVE {: RESULT = LLAVE; :}
                |   {: RESULT = null; :};
LLAVE           ::= allave LLLAVES:LLLAVES cllave {: RESULT = LLLAVES; :}
                |   allave LVALOR:LVALOR cllave {: RESULT = LVALOR; :};
LLLAVES         ::= LLLAVES:LLLAVE coma LLAVE:LLAVE
                    {:
                        if(LLAVE != null)
                            LLLAVE.hijos.add(LLAVE);
                        RESULT = (Nodo) LLLAVE;
                    :}
                |   LLAVE:LLAVE
                    {: 
                        RESULT = new Nodo(Const.lllave);
                        if(LLAVE != null)
                            RESULT = NodoGeneral.crearLlave(LLAVE);
                    :};

PRINCIPAL       ::= vacio inicio aparentesis cparentesis allave LCUERPO:LCUERPO cllave
                    {:
                        RESULT = NodoGeneral.crearPrincipal(LCUERPO);
                    :};

LCUERPO         ::= LCUERPO:LCUERPO CUERPO:CUERPO
                    {:
                        if(CUERPO != null)
                            LCUERPO.hijos.add(CUERPO);
                        RESULT = (Nodo) LCUERPO;
                    :}
                |   CUERPO:CUERPO 
                    {: 
                        RESULT = new Nodo(Const.lcuerpo);
                        if(CUERPO != null)
                            RESULT = NodoGeneral.crearCuerpo(CUERPO);
                    :};

CUERPO          ::= DEC:t1 interroga {: RESULT = t1; :}
                |   ASIGNACION:t1 interroga {: RESULT = t1; :}
                |   SI:t1 {: RESULT = t1; :}
                |   SELECCION:t1 {: RESULT = t1; :}
                |   PARA:t1 {: RESULT = t1; :}
                |   MIENTRAS:t1 {: RESULT = t1; :}
                |   HACER:t1 {: RESULT = t1; :}
                |   LLAMADO:t1 interroga {: RESULT = t1; :}
                |   LLAMADOHK:t1 interroga {: RESULT = t1; :}
                |   GRAFICAR:t1 interroga {: RESULT = t1; :}
                |   IMPRIMIR:t1 interroga {: RESULT = t1; :}
                |   LID:LID OP:OP interroga {: RESULT = NodoGeneral.crearAumento(LID, OP); :}
                |   retornar VALOR:t1 interroga {: RESULT = new Nodo(Const.retornar); RESULT.hijos.add(t1); :}
                |   terminar interroga {: RESULT = new Nodo(Const.terminar); :}
                |   continuar interroga {: RESULT = new Nodo(Const.continuar); :}
                |   error interroga;

ASIGNACION      ::= LID:LID LCORCHETES:LCORCHETES igual VALOR:VALOR {: RESULT = NodoGeneral.crearAsignacion(LID, VALOR, LCORCHETES); :}
                |   LID:LID igual VALOR:VALOR {: RESULT = NodoGeneral.crearAsignacion(LID, VALOR, null); :}
                |   LID:LID igual nuevo iden:id aparentesis cparentesis {: Nodo nuevo = new Nodo(Const.nuevo, id); RESULT = NodoGeneral.crearAsignacion(LID, nuevo, null); :};

SI              ::= si aparentesis VALOR:VALOR cparentesis allave LCUERPO:LCUERPO cllave SINO:SINO
                    {:
                        RESULT = NodoGeneral.crearIf(VALOR, LCUERPO, SINO);
                    :};
SINO            ::= sino allave LCUERPO:LCUERPO cllave {: RESULT = LCUERPO; :}
                |   {: RESULT = null; :};
SELECCION       ::= seleccion aparentesis ACCESO:ACCESO cparentesis allave LCASOS:LCASOS DEFECTO:DEFECTO cllave
                    {:
                        RESULT = NodoGeneral.crearSeleccion(ACCESO, LCASOS, DEFECTO);
                    :};

LCASOS          ::= LCASOS:LCASOS CASO:CASOS 
                    {:
                        LCASOS.hijos.add(CASOS);
                        RESULT = (Nodo) LCASOS;
                    :}
                |   CASO:CASO {: RESULT = NodoGeneral.crearLcasos(CASO); :};
CASO            ::= caso VALOR:VALOR dospuntos LCUERPO:LCUERPO {: RESULT = NodoGeneral.crearCaso(VALOR, LCUERPO); :};
DEFECTO         ::= defecto dospuntos LCUERPO:LCUERPO {: RESULT = NodoGeneral.crearDefecto(LCUERPO); :}
                |   {: RESULT = null; :};
PARA            ::= para aparentesis ASIGPARA:ASIGPARA dospuntos VALOR:VALOR dospuntos VALPARA:VAL cparentesis allave LCUERPO:LCUERPO cllave
                    {:
                        RESULT = NodoGeneral.crearPara(ASIGPARA, VALOR, VAL, LCUERPO);
                    :};
ASIGPARA        ::= ASIGNACION:ASIG {: RESULT = ASIG; :}
                |   var TIPO:TIPO iden:id igual VALOR:VALOR
                    {:
                        RESULT = NodoGeneral.crearDec(id, TIPO, Const.publico, VALOR);
                    :};
VALPARA         ::= LID:LID OP:OP {: RESULT = NodoGeneral.crearAumento(LID, OP); :}
                |   ASIGNACION:ASIG {: RESULT = ASIG; :};
OP              ::= masmas {: RESULT = Const.masmas; :}
                |   menosmenos {: RESULT = Const.menosmenos; :};

MIENTRAS        ::= mientras aparentesis VALOR:VALOR cparentesis allave LCUERPO:LCUERPO cllave
                    {:
                        RESULT = NodoGeneral.crearMientras(VALOR, LCUERPO);
                    :};

HACER           ::= hacer allave LCUERPO:LCUERPO cllave mientras aparentesis VALOR:VALOR cparentesis
                    {:
                        RESULT = NodoGeneral.crearHacer(VALOR, LCUERPO);
                    :};

GRAFICAR        ::= graphikarfuncion aparentesis LID:LID1 coma LID:LID2 cparentesis
                    {:
                        RESULT = NodoGeneral.crearGraficar(LID1, LID2);
                    :};

LLAMADO         ::= llamar LID:LID {: RESULT = NodoGeneral.crearLlamar(LID); :};

LLAMADOHK       ::= llamarhk iden:id aparentesis LVALOR:LVALOR cparentesis {: RESULT = NodoGeneral.crearLlamado(id, LVALOR); :};

IMPRIMIR        ::= imprimir aparentesis VALOR:VALOR cparentesis {: RESULT = NodoGeneral.crearImprimir(VALOR); :};

E               ::= E:t1 mas E:t2 {: RESULT = NodoOperacion.crearNodo(Const.mas, t1, t2); :}
                |   E:t1 menos E:t2 {: RESULT = NodoOperacion.crearNodo(Const.menos, t1, t2); :}
                |   E:t1 por E:t2 {: RESULT = NodoOperacion.crearNodo(Const.por, t1, t2); :}
                |   E:t1 dividido E:t2 {: RESULT = NodoOperacion.crearNodo(Const.dividido, t1, t2); :}
                |   E:t1 pot E:t2 {: RESULT = NodoOperacion.crearNodo(Const.pot, t1, t2); :}
                |   menos E:t1 {: RESULT = NodoOperacion.crearNodo(Const.menos, t1, null); :}
                |   aparentesis VALOR:t1 cparentesis {: RESULT = t1; :}
                |   numero:t1 {: RESULT = NodoGeneral.crearHoja(Const.numero, t1, Const.tnumero); :}
                |   dec:t1 {: RESULT = NodoGeneral.crearHoja(Const.decimal, t1, Const.tdecimal); :}
                |   verdadero:t1 {: RESULT = NodoGeneral.crearHoja(Const.verdadero, t1, Const.tbool); :}
                |   falso:t1 {: RESULT = NodoGeneral.crearHoja(Const.falso, t1, Const.tbool); :}
                |   car:t1 {: RESULT = NodoGeneral.crearHoja(Const.caracter, t1, Const.tcaracter); :}
                |   cad:t1 {: RESULT = NodoGeneral.crearHoja(Const.cadena, t1, Const.tcadena); :}
                |   ACCESO:t1 {: RESULT = t1; :}
                |   LLAMADO:t1 {: RESULT = t1; :}
                |   LLAMADOHK:t1 {: RESULT = t1; :};

VALOR           ::= VALOR:t1 or VALOR:t2 {: RESULT = NodoOperacion.crearNodo(Const.or, t1, t2); :}
                |   VALOR:t1 xor VALOR:t2 {: RESULT = NodoOperacion.crearNodo(Const.xor, t1, t2); :}
                |   VALOR:t1 and VALOR:t2 {: RESULT = NodoOperacion.crearNodo(Const.and, t1, t2); :}
                |   not VALOR:t1 {: RESULT = NodoOperacion.crearNodo(Const.not, t1, null); :}
                |   REL:REL {: RESULT = REL; :};

REL             ::= E:t1 OPREL:op E:t2 {: RESULT = NodoOperacion.crearRelacional(op, t1, t2); :}
                |   E:E {: RESULT = E; :};

ACCESO          ::= LID:LID LCORCHETES:LCORCHETES
                    {:
                        LID.hijos.add(LCORCHETES);
                        RESULT = (Nodo) LID;
                    :}
                |   LID:LID {: RESULT = LID; :};

OPREL           ::= menor {: RESULT = Const.menor; :}
                |   mayor {: RESULT = Const.mayor; :}
                |   menorigual {: RESULT = Const.menorigual; :}
                |   mayorigual {: RESULT = Const.mayorigual; :}
                |   igualigual {: RESULT = Const.igualigual; :}
                |   diferente {: RESULT = Const.diferente; :};